//managed implementation in class zbp_ats_rv_aroy_travel unique;
managed implementation in class zbp_ats_aroy_travel unique;
strict ( 2 );

define behavior for zats_rv_aroy_travel alias Travel
implementation in class zbp_ats_aroy_travel unique
persistent table /dmo/travel_m
lock master
early numbering
authorization master ( instance )
with additional save
etag master LastChangedAt
{
  create;
  update;
  delete;

  association _Booking { create ( features : instance ); }

  //static feature control
  field ( readonly ) TravelId, CreatedAt, CreatedBy, LastChangedAt, LastChangedBy, TotalPrice;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, CurrencyCode, OverallStatus, EndDate;

  factory action copyTravel [1];

  internal action reCalcTotalPrice;

  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  validation validateHeaderData on save { create; field CustomerId, BeginDate, EndDate; }

  side effects
  {
    field BookingFee affects field TotalPrice;
    field CurrencyCode affects field TotalPrice;
  }

  mapping for /dmo/travel_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      CreatedAt     = created_at;
      CreatedBy     = created_by;
      TotalPrice    = total_price;
      OverallStatus = overall_status;
      Description   = description;
      CurrencyCode  = currency_code;
      LastChangedAt = last_changed_at;
      LastChangedBy = last_changed_by;
      CustomerId    = customer_id;
    }
}

define behavior for zats_iv_aroy_booking alias Booking
implementation in class zbp_ats_aroy_booking unique
persistent table /dmo/booking_m
lock dependent by _Travel
early numbering
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  association _Travel;
  association _BookingSupplement { create; }

  //Static feature control
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, FlightPrice, CurrencyCode, CustomerId;

  determination calculateTotalPrice on modify { field FlightPrice, CurrencyCode; }

  mapping for /dmo/booking_m
    {
      BookingId     = booking_id;
      TravelId      = travel_id;
      BookingStatus = booking_status;
      CarrierId     = carrier_id;
      FlightDate    = flight_date;
      ConnectionId  = connection_id;
      CurrencyCode  = currency_code;
      FlightPrice   = flight_price;
      LastChangedAt = last_changed_at;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
    }
}

define behavior for zats_iv_aroy_booking_supl alias BookingSuppl
implementation in class zbp_ats_aroy_booksupl unique
persistent table /dmo/booksuppl_m
lock dependent by _Travel
early numbering
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  association _Travel;
  association _Booking;

  //Static Feature Control
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) CurrencyCode, Price, SupplementId;

  mapping for /dmo/booksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}